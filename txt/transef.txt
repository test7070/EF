transef2tre:--transef2tre
	SET QUOTED_IDENTIFIER OFF
	declare @t_noa nvarchar(max)= N[1]
	declare @t_driverno nvarchar(max)= N[2]
	declare @t_bdate nvarchar(20) = N[3]
	declare @t_edate nvarchar(20) = N[4]
	declare @t_btrandate nvarchar(20)= N[5]
	declare @t_etrandate nvarchar(20)= N[6]
	
	set @t_edate = case when len(ISNULL(@t_edate,''))=0 then CHAR(255) else @t_edate end
	set @t_etrandate = case when len(ISNULL(@t_etrandate,''))=0 then CHAR(255) else @t_etrandate end
	
	select a.*,isnull(a.price2,0)+isnull(a.price3,0) tremoney
	from view_transef a
	left join view_tres b on a.noa=b.tranno
	where a.driverno=@t_driverno
	and (b.noa is null or b.noa=@t_noa)
	and isnull(a.datea,'') between @t_bdate and @t_edate
	and ISNULL(a.trandate,'') between @t_btrandate and @t_etrandate;

transef2trd:--transef2trd
	SET QUOTED_IDENTIFIER OFF
	declare @t_noa nvarchar(20)= N[1]
	declare @t_custno nvarchar(20)= N[2]
	declare @t_bdate nvarchar(20)= N[3]
	declare @t_edate nvarchar(20)= N[4]
	declare @t_btrandate nvarchar(20)= N[5]
	declare @t_etrandate nvarchar(20)= N[6]
	
	set @t_edate = case when len(ISNULL(@t_edate,''))=0 then CHAR(255) else @t_edate end
	set @t_etrandate = case when len(ISNULL(@t_etrandate,''))=0 then CHAR(255) else @t_etrandate end

	select a.* 
	from view_transef a
	left join view_trds b on a.noa=b.tranno
	where ISNULL(a.custno,'')=@t_custno
	and ISNULL(a.datea,'') between @t_bdate and @t_edate
	and ISNULL(a.trandate,'') between @t_btrandate and @t_etrandate
	and (b.noa is null or @t_noa=b.noa)
	and ISNULL(a.price,0)!=0;

transef_copy:--transef_copy 
	SET QUOTED_IDENTIFIER OFF
	declare @cmd nvarchar(max)
	declare @workerno nvarchar(20) = N[1]
	declare @worker nvarchar(20) = N[2]
	declare @t_noa nvarchar(20) = N[3]
	declare @t_bdate nvarchar(10) = N[4]
	declare @t_edate nvarchar(10) = N[5]
	declare @t_weekday nvarchar(max) = [6] --日一二三四五六  => 1234567
	-----------------------------------------------------------------------------
	if not exists(select * from view_transef where noa=@t_noa)
	begin
		select '單據不存在' errmsg
		return
	end
	if exists(select COUNT(1) from view_transef where noa=@t_noa having COUNT(1)>1)
	begin
		select '單據重複' errmsg
		return
	end
	--------------------------------------------------------------------------------
	declare @tmpa table(
		datea nvarchar(20),
		[day] int,
		noa nvarchar(20)
	)	
	begin try
		declare @bdate date = cast(cast(cast(left(@t_bdate,3) as int)+1911 as nvarchar)+'/'+right(@t_bdate,5) as date)
		declare @edate date = cast(cast(cast(left(@t_edate,3) as int)+1911 as nvarchar)+'/'+right(@t_edate,5) as date)
		declare @nextdate date = @bdate
		while @nextdate<=@edate
		begin
			insert into @tmpa(datea,[day])
			select right('000'+cast(YEAR(@nextdate)-1911 as nvarchar),3)+'/'+right('00'+cast(MONTH(@nextdate) as nvarchar),2)+'/'+right('00'+cast(day(@nextdate) as nvarchar),2)
			,DATEPART(WEEKDAY, @nextdate)
			set @nextdate = dateadd(DD,1,@nextdate)
		end
		delete @tmpa where CHARINDEX(CAST([day] as nvarchar),@t_weekday)=0
	end try
	begin catch
		select '日期異常' errmsg
		return
	end catch
	-----------------------------------------------------------------------------------
	declare @noa nvarchar(20)
	declare @maxnoa nvarchar(20)
	declare @xdatea nvarchar(20)
	declare @accy nvarchar(20)
	----------------------------------------------------
	--排除本張出車單
	select @xdatea=datea from view_transef where noa=@t_noa
	delete @tmpa where datea =@xdatea
	----------------------------------------------------
	declare @number nvarchar(max)
	declare @string nvarchar(max) = '0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'
	declare @nn int
	declare @mm nvarchar(max)
	declare @key nvarchar(max)
	
	declare cursor_table cursor for
	select datea from @tmpa
	open cursor_table
	fetch next from cursor_table
	into @xdatea
	while(@@FETCH_STATUS <> -1)
	begin
		set @maxnoa = ''
		select top 1 @maxnoa = noa from view_transef where noa like 'BA'+REPLACE(@xdatea,'/','')+'[0-9,A-Z][0-9][0-9]' order by noa desc
		if len(ISNULL(@maxnoa,''))=0
		begin
			set @maxnoa = 'BA'+REPLACE(@xdatea,'/','')+'001'
		end
		else
		begin
			set @number  = right(@maxnoa,3)				
			set @nn = (charindex(left(@number,1),@string)-1) * 100 + cast(right(@number,2) as int) + 1
			set @mm = substring(@string,floor(@nn/100)+1,1)
			set @maxnoa=  'BA'+REPLACE(@xdatea,'/','')+@mm+RIGHT('00'+cast(@nn as nvarchar),2)
		end
		if exists(select * from view_transef where noa=@maxnoa)
		begin
			select '【'+@maxnoa+'】單號已存在' errmsg
			return
		end
		update @tmpa set noa=@maxnoa where datea=@xdatea

		fetch next from cursor_table
		into @xdatea
	end
	close cursor_table
	deallocate cursor_table
	--------------------------------------------------------------
	declare cursor_table cursor for
	select datea,noa from @tmpa
	open cursor_table
	fetch next from cursor_table
	into @xdatea,@noa
	while(@@FETCH_STATUS <> -1)
	begin
		set @accy = LEFT(@xdatea,3)
		
		set @cmd = " insert into transef"+@accy+"(worker,noa,noq,custno,comp,nick,driverno,driver,carno,straddrno,endaddrno,straddr,endaddr,trandate,datea,inmount,pton,mount,price,total,outmount,pton2,mount2,price2,price3,discount,total2,calctype,carteamno"
			+" ,po,unit,sender,stel,saddr,addressee,atel,aaddr,miles,tggno,tgg,memo)"
			+" select @worker,@noa,'001',custno,comp,nick,driverno,driver,carno,straddrno,endaddrno,straddr,endaddr,@xdatea,@xdatea,inmount,pton,mount,price,total,outmount,pton2,mount2,price2,price3,discount,total2,calctype,carteamno"
			+" ,po,unit,sender,stel,saddr,addressee,atel,aaddr,miles,tggno,tgg,'出車單【'+@t_noa+'】複製'"
			+" from view_transef where noa=@t_noa"
		execute sp_executesql @cmd,N'@worker nvarchar(20),@t_noa nvarchar(20),@noa nvarchar(20),@xdatea nvarchar(20)'
			,@worker=@worker,@t_noa=@t_noa,@noa=@noa,@xdatea=@xdatea
		fetch next from cursor_table
		into @xdatea,@noa 
	end
	close cursor_table
	deallocate cursor_table
	
	--寫入DRUN
	insert into drun(datea,timea,usera,action,noa,tablea,title)
	select convert(nvarchar,getdate(),111),left(convert(nvarchar,getdate(),108),5)
		,@workerno,'Insert',noa,'transef','出車單複製'
	from @tmpa
	
	select * from @tmpa;
